win_width = Window.GetWidth();
win_height = Window.GetHeight();
ww_half = win_width / 2;
wh_half = win_height / 2;

sunheight_offset = 55;

sourceImage = Image ("xbmc_black.png");
newxbmc_width = sourceImage.GetWidth() / (win_width / ww_half);
newxbmc_height = sourceImage.GetHeight() / (win_height / wh_half);
scaledImage = sourceImage.Scale (newxbmc_width, newxbmc_height);
xbmcBlack.sprite = Sprite (scaledImage);
xbmcBlack.sprite.SetY (wh_half - (newxbmc_height / 2));
xbmcBlack.sprite.SetOpacity (1);

sourceImage = Image ("sun_glow.png");
newsun_width = sourceImage.GetWidth() / (win_width / ww_half);
newsun_height = sourceImage.GetHeight() / (win_height / wh_half);
scaledImage = sourceImage.Scale (newsun_width, newsun_height);
sunGlow.sprite = Sprite (scaledImage);
sunGlow.sprite.SetY (wh_half - (newxbmc_height / 2) + sunheight_offset);
sunGlow.sprite.SetOpacity (1);

sourceImage = Image ("confluence.png");
newconfluence_width = sourceImage.GetWidth() / (win_width / ww_half); 
newconfluence_height = sourceImage.GetHeight() / (win_height / wh_half);
scaledImage = sourceImage.Scale (newconfluence_width, newconfluence_height);
confluence.sprite = Sprite (scaledImage);
confluence.sprite.SetX (ww_half + (ww_half / 2));
confluence.sprite.SetOpacity (1);

xbmcBlack.animStart = 0;
xbmcBlack.animEnd = 1.0;
xbmcBlack.startPos = win_width - (ww_half / 2);
xbmcBlack.endPos = ww_half - (newxbmc_width / 2); 

sunGlow.animStart = 0;
sunGlow.animEnd = 1.0;
sunGlow.startPos = (ww_half / 2);
sunGlow.endPos = ww_half + (newxbmc_width / 2) - (newsun_width / 2);

confluence.animStart = 0;
confluence.animEnd = 1;
confluence.startPos = wh_half;// + (wh_half / 2);
confluence.endPos = wh_half + (wh_half / 2);

fun remap (val, imin, imax, omin, omax) {
    return (((val - imin) / (imax - imin)) * (omax - omin)) + omin;
}

fun progress_callback (d, p) {

    p = (p * 100.0) / 15.0;

    if (p >= xbmcBlack.animStart && p <= xbmcBlack.animEnd) {
        a = remap (p, xbmcBlack.animStart, xbmcBlack.animEnd, 0, 1);
        b = xbmcBlack.startPos - ((xbmcBlack.startPos - xbmcBlack.endPos) * a);
        xbmcBlack.sprite.SetX (b);
    }
    if (p >= sunGlow.animStart && p <= sunGlow.animEnd) {
        a = remap (p, sunGlow.animStart, sunGlow.animEnd, 0, 1);
        b = sunGlow.startPos - ((sunGlow.startPos - sunGlow.endPos) * a);
        sunGlow.sprite.SetX (b);
    }
    if (p >= confluence.animStart && p <= confluence.animEnd) {
        a = remap (p, confluence.animStart, confluence.animEnd, 0, 1);
        b = confluence.startPos - ((confluence.startPos - confluence.endPos) * a);
        confluence.sprite.SetY (b);
    }
          
}
Plymouth.SetBootProgressFunction(progress_callback);
